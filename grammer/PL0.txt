/todo 语句


[done]<程序>::=<分程序>.
[done]<分程序> ::= [<常量说明部分>][变量说明部分>][<过程说明部分>]<语句>
                First  const
                       var
                       procedure
                       <语句>

[done]<常量说明部分> ::= const <常量定义>{,<常量定义>};
[done]<常量定义> ::= <标识符>=<无符号整数>
[done]<无符号整数> ::= <数字>{<数字>}
[done]<标识符> ::= <字母>{<字母>|<数字>}
[done]<变量说明部分>::= var <标识符>{,<标识符>};
[done]<过程说明部分> ::= <过程首部><分程序>;{<过程说明部分>}
[done]<过程首部> ::= procedure <标识符>;
[done]<语句> ::= <赋值语句>|<条件语句>|<当型循环语句>|<过程调用语句>|<读语句>|<写语句>|<复合语句>|<重复语句>|<空>
                First   <标识符>-><赋值语句>
                        if     -><条件语句>
                        while  -><当型循环语句>
                        call   -><过程调用语句>
                        read
                        write
                        begin  -><复合语句>
                        repeat -><重复语句>
[done]<赋值语句> ::= <标识符>:=<表达式>
[done]<表达式> ::= [+|-]<项>{<加法运算符><项>}
                First   +
                        -
                        <项>
[done]<项> ::= <因子>{<乘法运算符><因子>}
                First   <因子>
[done]<因子> ::= <标识符>|<无符号整数>|'('<表达式>')'
                First   <标识符>
                        <无符号整数>
                        (
[done]<加法运算符> ::= +|-
[done]<乘法运算符> ::= *|/
[done]<条件> ::= <表达式><关系运算符><表达式>|odd <表达式>
[done]<关系运算符> ::= =|<>|<|<=|>|>=
[done]<条件语句> ::= if<条件>then<语句>[else<语句>]
[done]<当型循环语句> ::= while<条件>do<语句>
[done]<过程调用语句> ::= call <标识符>
[done]<复合语句> ::= begin<语句>{;<语句>}end
[done]<重复语句> ::= repeat<语句>{;<语句>}until<条件>
[done]<读语句> ::= read'('<标识符>{,<标识符>}')'
[done]<写语句> ::= write'('<标识符>{,<标识符>}')'
<字母> ::= a|b|...|X|Y|Z
<数字> ::= 0|1|2|...|8|9